plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
}
//版本
def versionMajor    = 1
def versionMinor    = 0
def versionPatch    = 3

android {
    namespace 'com.firechun.dialog'
    compileSdk 33

    defaultConfig {
        minSdk 24
        targetSdk 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    //生成aar
    libraryVariants.all { variant ->
        def name = "dialog-${versionMajor}.${versionMinor}.${versionPatch}.aar"
        variant.outputs.all {
            outputFileName = name
        }
        //打包完成后的动作
        variant.assembleProvider.get().doLast {
            File out = new File("${project.rootDir}/maven-dialog")
            variant.outputs.all {
                def provider = variant.packageLibraryProvider.get()
                def file = provider.destinationDirectory.getAsFile().get()
                copy {
                    from file
                    into out
                }
            }
        }
    }
}
ext {
    PUBLISH_ARTIFACT_ID = "dialog" //你的artifact_id，一般是module的名字
    PUBLISH_VERSION = "$versionMajor.$versionMinor.$versionPatch"  //发布版本号
    PUBLISH_GROUP_ID = 'io.github.firechun'  //你的sonatype的group_id
}
apply from: '../publish-dialog.gradle'

dependencies {
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}